let
  pynixifyOverlay =
    self: super: {
      python39 = super.python39.override { inherit packageOverrides; };
      python310 = super.python310.override { inherit packageOverrides; };
    };

  packageOverrides = self: super: with self; {
    inherit (super.stdenv) isDarwin isAarch64 isNixOS;
    isM1 = isDarwin && isAarch64;
    isOldMac = isDarwin && !isAarch64;
    # fixes for mac
    dnspython =
      if self.isDarwin then
        super.dnspython.overrideAttrs
          (_: {
            disabledTestPaths =
              [
                "tests/test_async.py"
                "tests/test_query.py"
                "tests/test_resolver.py"
                "tests/test_resolver_override.py"
              ];
          }) else super.dnspython;
    httplib2 =
      if self.isOldMac then
        super.httplib2.overrideAttrs
          (_: {
            disabledTests =
              [
                "test_connection_close"
                "test_timeout_subsequent"
                "test_client_cert_password_verified"
              ];
          }) else super.httplib2;
    passlib =
      if self.isDarwin then
        super.passlib.overrideAttrs
          (_: {
            doCheck = false;
          }) else super.passlib;

    # my packages
    archives = buildPythonPackage rec {
      pname = "archives";
      version = "0.12";

      src = fetchPypi {
        inherit pname version;
        sha256 = "10frsfmbd8cc8dv3dayfd68msk8ah0kvlr2yyx5y7l1vrmcsgxy8";
      };

      propagatedBuildInputs = [ click typed-ast radon ];

      meta = with lib; {
        description = "a new way to do python code documentation";
        homepage = "https://github.com/jpetrucciani/archives.git";
      };
    };

    # requirements for other packages
    radon = buildPythonPackage rec {
      pname = "radon";
      version = "5.1.0";

      src = fetchPypi {
        inherit pname version;
        sha256 = "1vmf56zsf3paa1jadjcjghiv2kxwiismyayq42ggnqpqwm98f7fb";
      };

      propagatedBuildInputs = [ mando colorama future ];

      doCheck = false;

      meta = with lib; {
        description = "Code Metrics in Python";
        homepage = "https://radon.readthedocs.org/";
      };
    };

    mando = buildPythonPackage rec {
      pname = "mando";
      version = "0.6.4";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0q6rl085q1hw1wic52pqfndr0x3nirbxnhqj9akdm5zhq2fv3zkr";
      };

      propagatedBuildInputs = [ six ];

      doCheck = false;

      meta = with lib; {
        description = "Create Python CLI apps with little to no effort at all!";
        homepage = "https://mando.readthedocs.org/";
      };
    };

    # type annotations
    boto3-stubs = buildPythonPackage rec {
      pname = "boto3-stubs";
      version = "1.20.35";

      src = fetchPypi {
        inherit pname version;
        sha256 = "1nnd8jjakbcfjsfwn0w7i8mkqj7zji7x2vzmgklbrh3hw10ig95p";
      };

      propagatedBuildInputs = [ botocore-stubs ];
      checkInputs = [
        boto3
      ];
      pythonImportsCheck = [
        "boto3-stubs"
      ];

      meta = with lib; {
        description =
          "Type annotations for boto3 1.20.35, generated by mypy-boto3-builder 6.3.1";
        homepage = "https://github.com/vemel/mypy_boto3_builder";
      };
    };

    botocore-stubs = buildPythonPackage rec {
      pname = "botocore-stubs";
      version = "1.24.6";

      src = fetchPypi {
        inherit pname version;
        sha256 = "093zsj2wk7xw89yvs7w88z9w3811vkpgfv4q3wk9j6gd6n3hr1pw";
      };

      pythonImportsCheck = [
        "botocore-stubs"
      ];

      meta = with lib; {
        description =
          "Type annotations for botocore 1.24.6 generated with mypy-boto3-builder 7.1.2";
        homepage = "https://github.com/vemel/mypy_boto3_builder";
      };
    };


    # njsscan
    njsscan = buildPythonPackage rec {
      pname = "njsscan";
      version = "0.3.1";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0slvk3h4flk3y95yh19p629xly37yqh42rfw1vqv598srj6x7j93";
      };

      propagatedBuildInputs = [
        colorama
        libsast
        sarif-om
        jschema-to-python
        tabulate
      ];

      pythonImportsCheck = [
        "njsscan"
      ];

      meta = with lib; {
        description =
          "njsscan is a SAST tool that can find insecure code patterns in your Node.js applications.";
        homepage = "https://github.com/ajinabraham/njsscan";
      };
    };

    libsast = buildPythonPackage rec {
      pname = "libsast";
      version = "1.5.0";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0nwms1jjb22w3f6rsyh9w4mpr54kab7a10hr02c0wlw7n1ai5k9d";
      };

      propagatedBuildInputs = [
        requests
        pyyaml
        semgrep
      ];

      pythonImportsCheck = [
        "libsast"
      ];

      meta = with lib; {
        description = "A generic SAST core built on top of semgrep and regex";
        homepage = "https://github.com/ajinabraham/libsast";
      };
    };

    sarif-om = buildPythonPackage rec {
      pname = "sarif-om";
      version = "1.0.4";

      src = fetchPypi {
        inherit version;
        pname = "sarif_om";
        sha256 = "167gb8xjm0310km3w1s12bqldbv7zyklkr4j5900vq4361ml2pyd";
      };

      buildInputs = [ pbr ];
      propagatedBuildInputs = [
        attrs
        pbr
      ];

      # doCheck = false;
      pythonImportsCheck = [
        "sarif_om"
      ];

      meta = with lib; { };
    };

    jschema-to-python = buildPythonPackage rec {
      pname = "jschema-to-python";
      version = "1.2.3";

      src = fetchPypi {
        inherit version;
        pname = "jschema_to_python";
        sha256 = "14cvaiwm56g0v6p7zviikaa5i9ln3yqy910jmp60hirhbpz19zvn";
      };

      checkInputs = [
        pytestCheckHook
      ];

      buildInputs = [ pbr ];
      propagatedBuildInputs = [
        attrs
        jsonpickle
        pbr
      ];

      pythonImportsCheck = [
        "jschema_to_python"
      ];

      meta = with lib; { };
    };

    semgrep = buildPythonPackage rec {
      pname = "semgrep";
      version = "0.80.0";

      SEMGREP_CORE_BIN = "${pkgs.semgrep-core}/bin/semgrep-core";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0k27wmqw3b4xar9svc31hg2dxdxck6rgrki9r0vi4nw4mjb6q5mm";
      };

      propagatedBuildInputs = [
        attrs
        colorama
        click-option-group
        jsonschema
        packaging
        peewee
        requests
        ruamel-yaml
        tqdm
        wcmatch
      ];

      doCheck = false;
      pythonImportsCheck = [
        "semgrep"
      ];

      meta = with lib; { };
    };


    # prospector
    prospector = buildPythonPackage rec {
      pname = "prospector";
      version = "1.7.6";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0m26pzbk7fhklqdk5j66y8qh7ac8x5q76hq6br1q6jnin5vh252r";
      };

      propagatedBuildInputs = [
        pyyaml
        dodgy
        mccabe
        pep8-naming
        pycodestyle
        pydocstyle
        pyflakes
        pylint-celery
        pylint-django
        pylint-flask
        pylint-plugin-utils
        pylint
        requirements-detector
        setoptconf-tmp
        setuptools
        toml

        # with_everything
        bandit
        frosted
        mypy
        pyroma
        vulture
      ];

      doCheck = false;

      meta = with lib; { homepage = "http://prospector.readthedocs.io"; };
    };
    frosted = buildPythonPackage rec {
      pname = "frosted";
      version = "1.4.1";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0l9bwcahjymc62j9qlcgxmms7naz8akqdadri7136jq67asd5rfi";
      };

      propagatedBuildInputs = [ pies ];

      doCheck = false;

      pythonImportsCheck = [
        "frosted"
      ];

      meta = with lib; {
        description = "A passive Python syntax checker";
        homepage = "https://github.com/timothycrosley/frosted";
      };
    };
    pies = buildPythonPackage rec {
      pname = "pies";
      version = "2.6.7";

      src = fetchPypi {
        inherit pname version;
        sha256 = "1sg4fmdq3qd02cqgwgl07mfsc51897z866ch819603qfrqink9z8";
      };

      doCheck = false;

      pythonImportsCheck = [
        "pies"
      ];

      meta = with lib; {
        description =
          "The simplest way to write one program that runs on both Python 2 and Python 3.";
        homepage = "https://github.com/timothycrosley/pies";
      };
    };
    pep8-naming = buildPythonPackage rec {
      pname = "pep8-naming";
      version = "0.10.0";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0fmzccbmr0jn9ynamdb9ly2ai8qs5qfk8alfgnzr3fbjvpwsbd7k";
      };

      propagatedBuildInputs = [ flake8-polyfill ];

      pythonImportsCheck = [
        "pep8ext_naming"
      ];

      meta = with lib; {
        description = "Check PEP-8 naming conventions, plugin for flake8";
        homepage = "https://github.com/PyCQA/pep8-naming";
      };
    };
    pylint-django = buildPythonPackage rec {
      pname = "pylint-django";
      version = "2.5.2";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0z6rhxarc3sv4kb04yhx0pyvwrnqszgimydf28xqllwj98mxhcqr";
      };

      checkInputs = [
        pytestCheckHook
      ];

      propagatedBuildInputs = [ pylint-plugin-utils pylint ];

      pythonImportsCheck = [
        "pylint_django"
      ];

      doCheck = false;

      meta = with lib; {
        description =
          "A Pylint plugin to help Pylint understand the Django web framework";
        homepage = "https://github.com/PyCQA/pylint-django";
      };
    };
    pylint-plugin-utils = buildPythonPackage rec {
      pname = "pylint-plugin-utils";
      version = "0.7";

      src = fetchPypi {
        inherit pname version;
        sha256 = "1p84l2azmbc64fgj9aj81zz1h6v0zq6r8b3mbkfib55f2q2vqj6f";
      };

      propagatedBuildInputs = [ pylint ];

      pythonImportsCheck = [
        "pylint_plugin_utils"
      ];
      doCheck = false;

      meta = with lib; {
        description = "Utilities and helpers for writing Pylint plugins";
        homepage = "https://github.com/PyCQA/pylint-plugin-utils";
      };
    };
    setoptconf-tmp = buildPythonPackage rec {
      pname = "setoptconf-tmp";
      version = "0.3.1";

      src = fetchPypi {
        inherit pname version;
        sha256 = "0y2pgpraa36wzlzkxigvmz80mqd3mzcc9wv2yx9bliqks7fhlj70";
      };

      propagatedBuildInputs = [ pyyaml ];

      pythonImportsCheck = [
        "setoptconf"
      ];


      meta = with lib; {
        description =
          "A module for retrieving program settings from various sources in a consistant method.";
        homepage = "https://github.com/carlio/setoptconf-tmp";
      };
    };


    notion-client = buildPythonPackage rec {
      pname = "notion-client";
      version = "0.9.0";

      src = fetchPypi {
        inherit pname version;
        sha256 = "004vx0fv7v12r18m1np1hjx9qnxgdk6aajsjhchvz0fyl2588f3l";
      };

      propagatedBuildInputs = [ httpx ];

      doCheck = false;

      pythonImportsCheck = [
        "notion_client"
      ];

      meta = with lib; {
        description = "Python client for the official Notion API";
        homepage = "https://github.com/ramnes/notion-sdk-py";
      };
    };

  };
in
pynixifyOverlay
